/*@ define:module:ownership
---
name: Ownership filter
description: |
  The "Ownership filter" and "Item spawn filter" settings in plugin config
  override ALL other filter rules. This breaks things in raids because items
  like Dawnbringer and CoX mob drops have weird ownership values.

  If you're using this filter, you should disable those two settings and let
  this module handle it instead.
*/

#define CONST_AREA_COX [3200, 5120, 0, 3391, 5759, 3]
#define CONST_AREA_TOB [3136, 4224, 0, 3327, 4479, 3]
#define CONST_AREA_TOA [3520, 5120, 0, 3967, 5439, 3]
#define CONST_AREA_RAID(_) (area:CONST_AREA_COX || area:CONST_AREA_TOB || area:CONST_AREA_TOA)
#define CONST_TAKEABLE(_) (accountType:0 || !ownership:2)

/*@ define:input:ownership
type: boolean
label: 'Hide untakeable items (no effect if account is non-ironman)'
*/
#define VAR_OWNERSHIP_HIDEOTHER true

/*@ define:input:ownership
type: boolean
label: 'Hide world spawns (ignores raids)'
*/
#define VAR_OWNERSHIP_HIDESPAWN false

/*@ define:input:ownership
type: stringlist
label: Ignore ownership value for these items
*/
#define VAR_OWNERSHIP_IGNORE ["Dawnbringer"]

rule (!CONST_TAKEABLE(0) && VAR_OWNERSHIP_HIDEOTHER && !name:VAR_OWNERSHIP_IGNORE) {
  hidden = true;
}

rule (!CONST_AREA_RAID(0) && VAR_OWNERSHIP_HIDESPAWN && ownership:0 && !name:VAR_OWNERSHIP_IGNORE) {
  hidden = true;
}
